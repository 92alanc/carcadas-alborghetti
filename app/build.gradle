apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-parcelize'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

android {
    compileSdkVersion 33

    defaultConfig {
        applicationId "com.ukdev.carcadasalborghetti"
        minSdkVersion 23
        targetSdkVersion 33
        multiDexEnabled true
    }

    signingConfigs {
        free {
            keyAlias "${soundboards_key_alias}"
            keyPassword "${soundboards_key_password}"
            storeFile file("${soundboards_free_store_file}")
            storePassword "${soundboards_store_password}"
        }

        paid {
            keyAlias "${soundboards_key_alias}"
            keyPassword "${soundboards_key_password}"
            storeFile file("${soundboards_paid_store_file}")
            storePassword "${soundboards_store_password}"
        }
    }

    buildTypes {
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "version"

    productFlavors {
        free {
            versionCode 56
            versionName "2023.1.0"
            dimension "version"
            signingConfig signingConfigs.free
        }

        paid {
            versionCode 57
            versionName "2023.1.0"
            dimension "version"
            applicationIdSuffix ".paid"
            signingConfig signingConfigs.paid
        }
    }

    sourceSets {
        main.java.srcDirs = ['src/main/kotlin']
        free.java.srcDirs = ['src/free/kotlin']
        paid.java.srcDirs = ['src/paid/kotlin']
        test.java.srcDirs = ['src/test/kotlin']
        testFree.java.srcDirs = ['src/testFree/kotlin']
        testPaid.java.srcDirs = ['src/testPaid/kotlin']
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    buildFeatures {
        viewBinding true
    }

    testOptions {
        unitTests.returnDefaultValues true
    }
}

dependencies {
    implementation platform('com.google.firebase:firebase-bom:31.2.0')

    implementation 'androidx.appcompat:appcompat:1.6.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"
    implementation 'com.google.android.material:material:1.8.0'
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    implementation 'com.google.firebase:firebase-storage-ktx'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "io.insert-koin:koin-core:$koin_version"
    implementation "io.insert-koin:koin-android:$koin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    freeImplementation "com.smaato.android.sdk:smaato-sdk-banner:$smaato_version"
    freeImplementation "com.smaato.android.sdk:smaato-sdk-native:$smaato_version"

    paidImplementation "androidx.room:room-runtime:$room_version"
    paidImplementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'io.mockk:mockk:1.13.3'
    testImplementation 'com.google.truth:truth:1.1.3'
    testImplementation 'com.squareup.okhttp3:mockwebserver:4.10.0'
}
