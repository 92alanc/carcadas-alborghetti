apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.firebase.crashlytics'

android {
    compileSdkVersion 30

    defaultConfig {
        applicationId "com.ukdev.carcadasalborghetti"
        minSdkVersion 16
        targetSdkVersion 30
        multiDexEnabled true
    }

    signingConfigs {
        free {
            keyAlias "${soundboards_key_alias}"
            keyPassword "${soundboards_key_password}"
            storeFile file("${soundboards_free_store_file}")
            storePassword "${soundboards_store_password}"
        }

        paid {
            keyAlias "${soundboards_key_alias}"
            keyPassword "${soundboards_key_password}"
            storeFile file("${soundboards_paid_store_file}")
            storePassword "${soundboards_store_password}"
        }
    }

    buildTypes {
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "version"

    productFlavors {
        free {
            versionCode 51
            versionName "2021.1.1"
            dimension "version"
            signingConfig signingConfigs.free
        }

        paid {
            versionCode 51
            versionName "2021.1.1"
            dimension "version"
            applicationIdSuffix ".paid"
            buildConfigField "String", "BASE_URL", '"https://api.dropboxapi.com/"'
            buildConfigField "String", "BASE_URL_DOWNLOADS", '"https://content.dropboxapi.com/"'
            buildConfigField "String", "ACCESS_TOKEN", "\"${soundboards_access_token}\""
            signingConfig signingConfigs.paid
        }
    }

    sourceSets {
        main.java.srcDirs = ['src/main/kotlin']
        free.java.srcDirs = ['src/free/kotlin']
        paid.java.srcDirs = ['src/paid/kotlin']
        test.java.srcDirs = ['src/test/kotlin']
        testFree.java.srcDirs = ['src/testFree/kotlin']
        testPaid.java.srcDirs = ['src/testPaid/kotlin']
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    androidExtensions {
        experimental true
    }

    testOptions {
        unitTests.returnDefaultValues true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.recyclerview:recyclerview:1.2.0-rc01'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'com.google.firebase:firebase-core:18.0.2'
    implementation 'com.google.firebase:firebase-crashlytics:17.4.1'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.koin:koin-core:$koin_version"
    implementation "org.koin:koin-android:$koin_version"
    implementation "org.koin:koin-androidx-scope:$koin_version"
    implementation "org.koin:koin-android-ext:$koin_version"
    implementation "org.koin:koin-androidx-ext:$koin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    freeImplementation "com.smaato.android.sdk:smaato-sdk-banner:$smaato_version"
    freeImplementation "com.smaato.android.sdk:smaato-sdk-native:$smaato_version"

    paidImplementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    paidImplementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    paidImplementation 'com.squareup.okhttp3:okhttp:4.9.0'
    paidImplementation 'com.google.code.gson:gson:2.8.6'
    paidImplementation "androidx.room:room-runtime:$room_version"
    paidImplementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'io.mockk:mockk:1.10.6'
    testImplementation 'com.google.truth:truth:1.1.2'
    testImplementation 'com.squareup.okhttp3:mockwebserver:4.9.0'
}

apply plugin: 'com.google.gms.google-services'