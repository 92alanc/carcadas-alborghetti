plugins {
    id 'com.android.application'
    id 'com.google.devtools.ksp'
    id 'com.google.firebase.crashlytics'
    id 'com.google.gms.google-services'
    id 'com.google.dagger.hilt.android'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
}

android {
    namespace 'com.ukdev.carcadasalborghetti'
    compileSdk 34

    defaultConfig {
        applicationId "com.ukdev.carcadasalborghetti"
        minSdk 23
        targetSdk 34
        multiDexEnabled true
    }

    signingConfigs {
        free {
            keyAlias "${carcadas_key_alias}"
            keyPassword "${carcadas_key_password}"
            storeFile file("${carcadas_free_store_file}")
            storePassword "${carcadas_store_password}"
        }

        paid {
            keyAlias "${carcadas_key_alias}"
            keyPassword "${carcadas_key_password}"
            storeFile file("${carcadas_paid_store_file}")
            storePassword "${carcadas_store_password}"
        }
    }

    buildTypes {
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions = ["version"]

    productFlavors {
        free {
            versionCode 58
            versionName "2023.3.0"
            dimension "version"
            signingConfig signingConfigs.free
        }

        paid {
            versionCode 59
            versionName "2023.3.0"
            dimension "version"
            applicationIdSuffix ".paid"
            signingConfig signingConfigs.paid
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
    }

    buildFeatures {
        viewBinding true
    }

    testOptions {
        unitTests.returnDefaultValues true
    }
}

dependencies {
    implementation platform(libs.firebase.bom)

    implementation libs.android.activity
    implementation libs.android.appcompat
    implementation libs.android.core
    implementation libs.android.fragment
    implementation libs.android.swiperefreshlayout
    implementation libs.android.material
    implementation libs.firebase.crashlytics
    implementation libs.firebase.storage
    implementation libs.hilt.android

    kapt libs.hilt.compiler

    freeImplementation libs.google.ads

    paidImplementation libs.room.ktx
    ksp libs.room.compiler

    testImplementation libs.junit
    testImplementation libs.mockk.android
    testImplementation libs.truth
    testImplementation libs.turbine
}
