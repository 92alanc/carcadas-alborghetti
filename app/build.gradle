plugins {
    id 'com.android.application'
    id 'com.google.devtools.ksp'
    id 'com.google.firebase.crashlytics'
    id 'com.google.gms.google-services'
    id 'com.google.dagger.hilt.android'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
}

android {
    namespace 'com.ukdev.carcadasalborghetti'
    compileSdk 34

    defaultConfig {
        applicationId "com.ukdev.carcadasalborghetti"
        minSdk 23
        targetSdk 34
        multiDexEnabled true
    }

    signingConfigs {
        free {
            keyAlias "${carcadas_key_alias}"
            keyPassword "${carcadas_key_password}"
            storeFile file("${carcadas_free_store_file}")
            storePassword "${carcadas_store_password}"
        }

        paid {
            keyAlias "${carcadas_key_alias}"
            keyPassword "${carcadas_key_password}"
            storeFile file("${carcadas_paid_store_file}")
            storePassword "${carcadas_store_password}"
        }
    }

    buildTypes {
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions = ["version"]

    productFlavors {
        free {
            versionCode 58
            versionName "2023.3.0"
            dimension "version"
            signingConfig signingConfigs.free
        }

        paid {
            versionCode 59
            versionName "2023.3.0"
            dimension "version"
            applicationIdSuffix ".paid"
            signingConfig signingConfigs.paid
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
    }

    buildFeatures {
        viewBinding true
    }

    testOptions {
        unitTests.returnDefaultValues true
    }
}

dependencies {
    implementation platform('com.google.firebase:firebase-bom:32.7.1')

    implementation 'androidx.activity:activity-ktx:1.8.2'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.fragment:fragment-ktx:1.6.2'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'com.google.android.material:material:1.11.0'
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    implementation 'com.google.firebase:firebase-storage-ktx'
    implementation "com.google.dagger:hilt-android:$hilt_version"

    kapt "com.google.dagger:hilt-compiler:$hilt_version"

    freeImplementation 'com.google.android.gms:play-services-ads:22.6.0'

    paidImplementation "androidx.room:room-ktx:$room_version"
    ksp "androidx.room:room-compiler:$room_version"

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'io.mockk:mockk:1.13.9'
    testImplementation 'com.google.truth:truth:1.3.0'
}
